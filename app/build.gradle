plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'androidx.navigation.safeargs'
    id 'com.google.firebase.appdistribution'
    id 'com.google.firebase.crashlytics'
    id 'com.google.gms.google-services'
}
apply from: file("$project.rootDir/config.gradle")
apply from: "$project.rootDir/keystore/keystore_config.gradle"

def appPropsFile = file("$project.rootDir/local.properties")
Properties appProps = new Properties()
appProps.load(new FileInputStream(appPropsFile))

android {
    def globalConfig = rootProject.ext

    namespace 'com.reteno.sample'
    compileSdk globalConfig.androidCompileSdkVersion

    defaultConfig {
        applicationId "com.reteno.sample"
        minSdk globalConfig.androidMinSdkVersion
        targetSdk globalConfig.androidTargetSdkVersion

        android.buildFeatures.buildConfig true

        buildConfigField "String", "API_ACCESS_KEY", appProps['reteno.access-key']
        versionCode System.getenv("BUILD_NUMBER") as Integer ?: globalConfig.versionCode
        versionName globalConfig.versionName
    }

    signingConfigs {
        release {
            storeFile file(keystore_release.storeFile)
            storePassword keystore_release.storePassword
            keyAlias keystore_release.keyAlias
            keyPassword keystore_release.keyPassword
        }
        debug {
            storeFile file(keystore_debug.storeFile)
            storePassword keystore_debug.storePassword
            keyAlias keystore_debug.keyAlias
            keyPassword keystore_debug.keyPassword
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            versionNameSuffix "-debug"

            firebaseAppDistribution {
                appId = "1:987851207632:android:862ec0f79ede137abc7fd5"
                serviceCredentialsFile = file(config.firebaseFile)
                artifactType = "APK"
                groups = "yalantis"
            }
        }

        qa {
            signingConfig signingConfigs.debug
            minifyEnabled false
            debuggable true
            versionNameSuffix "-qa"

            firebaseAppDistribution {
                appId = "1:987851207632:android:862ec0f79ede137abc7fd5"
                releaseNotesFile = "release-notes.txt"
                serviceCredentialsFile = file(config.firebaseFile)
                artifactType = "APK"
                groups = "yalantis"
            }
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            firebaseAppDistribution {
                appId = "1:987851207632:android:862ec0f79ede137abc7fd5"
                releaseNotesFile = "release-notes.txt"
                artifactType = "APK"
                groups = "yalantis, client"
                serviceCredentialsFile = file(config.firebaseFile)
            }
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    //implementation 'com.reteno:fcm:1.6.6-SNAPSHOT'
    implementation project(':RetenoSdkFcm')


    implementation platform("com.google.firebase:firebase-bom:31.1.0")
    implementation "com.google.firebase:firebase-messaging"
    implementation "com.google.firebase:firebase-messaging-ktx"
    implementation "com.google.firebase:firebase-crashlytics"
    implementation "com.google.firebase:firebase-analytics"

    implementation "androidx.navigation:navigation-fragment-ktx:2.5.3"
    implementation "androidx.navigation:navigation-ui-ktx:2.5.3"

    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "androidx.activity:activity-ktx:1.6.1"
    implementation "androidx.appcompat:appcompat:1.5.1"
    implementation "com.google.android.material:material:1.7.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"

    implementation 'androidx.work:work-runtime-ktx:2.8.1'

    implementation "com.github.bumptech.glide:glide:4.14.2"
    annotationProcessor "com.github.bumptech.glide:compiler:4.14.2"

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.2'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.14'
}